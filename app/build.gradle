apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
  compileSdkVersion 28
  defaultConfig {
    applicationId "com.raywenderlich.android.collagedroid"
    minSdkVersion 19
    targetSdkVersion 28
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

configurations {
  ktlint
}


task ktlint(type: JavaExec, group: "verification") {
  description = "Check Kotlin code style."
  main = "com.github.shyiko.ktlint.Main"
  classpath = configurations.ktlint
  args "src/**/*.kt"
  // to generate report in checkstyle format prepend following args:
  // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
  // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
  description = "Fix Kotlin code style deviations."
  main = "com.github.shyiko.ktlint.Main"
  classpath = configurations.ktlint
  args "-F", "src/**/*.kt"
}

dependencies {
  ktlint "com.github.shyiko:ktlint:0.23.1"
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

  def androidx_version = "1.1.0-beta01"
  implementation "androidx.appcompat:appcompat:$androidx_version"
  implementation "com.google.android.material:material:1.1.0-alpha07"
  implementation "androidx.vectordrawable:vectordrawable:$androidx_version"
  implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

  implementation 'com.theartofdev.edmodo:android-image-cropper:2.7.0'

  testImplementation 'junit:junit:4.12'
  androidTestImplementation "androidx.test:runner:1.2.0"
  androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"

}
